plugins {
    id 'java'
}

group 'one.cafebabe'
version '2021.5'

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def appName = "Samurai"
task packageMacApp(type: Exec) {
    workingDir './'

    commandLine('jpackage',
            '--verbose',
            '--add-modules', 'java.base,java.desktop,java.naming,jdk.internal.jvmstat,jdk.attach',
            '--main-class', "$mainClassName",
            '--main-jar', "${projectDir}/samurai-swing/build/libs/samurai-swing-${project.version}.jar",
            '--jlink-options', '--strip-native-commands',
            '--java-options',
            """-Xmx128m -Xms128m -XX:+UseSerialGC -Dsun.java2d.metal=true
               --add-exports=jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED
               --add-exports=jdk.attach/sun.tools.attach=ALL-UNNAMED
               -XX:ErrorFile=\$USER_HOME/java_error_in_idea_%p.log
               -XX:HeapDumpPath=\$USER_HOME/java_error_in_idea.hprof""",
            '--name', appName,
            '--app-version', "${project.version}",
            '--copyright', 'Yusuke Yamamoto',
            '--vendor', 'Yusuke Yamamoto',
            '--icon', 'package/samurai.icns',
            '--input', "${projectDir}/samurai-swing/build/",
            '--dest', './build/',
            '--type', 'dmg',
            '--mac-package-identifier', 'samurai.swing.Samurai',
            '--mac-signing-key-user-name', 'Yusuke Yamamoto (TX2Q55XRF7)',
            '--mac-sign',
            '--mac-entitlements', "${projectDir}/package/entitlement.plist",
    )
}

tasks.packageMacApp.dependsOn([':samurai-swing:jar'])

task notarizeMacApp(type: Exec) {
    workingDir './'

    commandLine('xcrun',
            'altool', '--notarize-app', '--primary-bundle-id'
            , 'one.cafebabe.Samurai', '-u', project.properties['APPLE_DEVELOPER_USER'] ?: "none", '-p', project.properties['APPLE_DEVELOPER_APP_PASSWORD'] ?: "none", '-f', "${appName}-${project.version}.dmg"
    )
}

task checkMacAppNotarized(type: Exec) {
    workingDir './'
    commandLine('spctl', '--assess', '-vvvv', '--ignore-cache', '--no-cache', "/Applications/${appName}.app")
}

task stapleMacApp(type: Exec) {
    workingDir './'

    commandLine('xcrun',
            'stapler', 'staple', "${appName}-${project.version}.dmg"
    )
}


task packageWinApp(type: Exec) {
    workingDir './'
    commandLine('jpackage',
            '--verbose',
            '--add-modules', 'java.base,java.desktop,java.naming,jdk.internal.jvmstat,jdk.attach',
            '--main-class', project.properties['mainClassName'],
            '--main-jar', "${projectDir}/samurai-swing/build/libs/samurai-swing-${project.version}.jar",
            '--jlink-options', '--strip-native-commands',
            '--java-options',
            """-Xmx128m -Xms128m -XX:+UseSerialGC
               --add-exports=jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED
               --add-exports=jdk.attach/sun.tools.attach=ALL-UNNAMED
               -XX:ErrorFile=\$USER_HOME/java_error_in_idea_%p.log
               -XX:HeapDumpPath=\$USER_HOME/java_error_in_idea.hprof""",
            '--name', appName,
            '--app-version', "${project.version}",
            '--copyright', 'Yusuke Yamamoto',
            '--vendor', 'Yusuke Yamamoto',
            '--type', 'app-image',
            '--icon', 'package/samurai.ico',
            '--input', "${projectDir}/samurai-swing/build/",
            '--dest', './build/',
    )
}
